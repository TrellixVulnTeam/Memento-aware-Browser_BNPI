# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/test/base/js2gtest.gni")
import("//third_party/closure_compiler/compile_js.gni")

assert(is_chromeos, "Telemetry Extension is Chrome OS only")
assert(!is_official_build,
       "Telemetry Extension is only built for unofficial builds")

static_library("telemetry_extension_ui") {
  sources = [
    "probe_service.cc",
    "probe_service.h",
    "telemetry_extension_ui.cc",
    "telemetry_extension_ui.h",
    "url_constants.cc",
    "url_constants.h",
  ]

  deps = [
    "//chromeos/components/telemetry_extension_ui/mojom",
    "//chromeos/constants",
    "//chromeos/resources:telemetry_extension_resources",
    "//content/public/browser",
    "//ui/webui",
  ]
}

js_type_check("closure_compile") {
  deps = [
    ":trusted",
    ":untrusted",
  ]
}

js_library("trusted") {
  sources = [ "resources/trusted.js" ]
  deps = [ "//chromeos/components/telemetry_extension_ui/mojom:mojom_js_library_for_compile" ]
}

js_library("untrusted") {
  sources = [ "resources/untrusted.js" ]
}

js2gtest("browser_tests_js") {
  test_type = "mojo_lite_webui"

  sources = [ "test/telemetry_extension_ui_browsertest.js" ]

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
}
